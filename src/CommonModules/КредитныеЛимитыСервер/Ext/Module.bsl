Функция ПолучитьОстатокЛимита(Период, Клиент) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КредитныеЛимитыСрезПоследних.Клиент КАК Клиент,
	               |	КредитныеЛимитыСрезПоследних.СуммаЛимита КАК СуммаЛимита
	               |ПОМЕСТИТЬ КредитныйЛимит
	               |ИЗ
	               |	РегистрСведений.КредитныеЛимиты.СрезПоследних(&Период, Клиент = &Клиент) КАК КредитныеЛимитыСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗадолженностьКонтрагентовОстатки.Клиент КАК Клиент,
	               |	ЗадолженностьКонтрагентовОстатки.СуммаОстаток КАК СуммаОстаток
	               |ПОМЕСТИТЬ Задолженность
	               |ИЗ
	               |	РегистрНакопления.ЗадолженностьКонтрагентов.Остатки(&Период, Клиент = &Клиент) КАК ЗадолженностьКонтрагентовОстатки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КредитныйЛимит.Клиент КАК Клиент,
	               |	КредитныйЛимит.СуммаЛимита КАК СуммаЛимита,
	               |	ЕСТЬNULL(Задолженность.СуммаОстаток, 0) КАК СуммаЗадолженности,
	               |	КредитныйЛимит.СуммаЛимита - ЕСТЬNULL(Задолженность.СуммаОстаток,0) КАК ОстатокЛимита
	               |ИЗ
	               |	КредитныйЛимит КАК КредитныйЛимит
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Задолженность КАК Задолженность
	               |		ПО КредитныйЛимит.Клиент = Задолженность.Клиент";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Клиент", Клиент);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
	
		Возврат Выборка.ОстатокЛимита 
	Иначе
		Возврат 0;
	
	КонецЕсли;

КонецФункции // ПолучитьОстатокЛимита()
